apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    def versionMajor = rootProject.ext.versionMajor
    def versionMinor = rootProject.ext.versionMinor
    def versionPatch = rootProject.ext.versionPatch

    defaultConfig {
        applicationId "com.devabit.takestock"

        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        archivesBaseName = "takestock-$versionName"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            // Run code coverage reports by default on debug builds.
            // testCoverageEnabled = true
        }
    }

    productFlavors {
        dev {
            minSdkVersion 21
        }
        mock {
            applicationIdSuffix = ".mock"
        }
        prod {
            // The actual minSdkVersion for the application.
            minSdkVersion rootProject.ext.minSdkVersion
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

dependencies {

    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"

//    compile 'io.reactivex:rxjava:1.1.2'
//    compile 'io.reactivex:rxandroid:1.1.0'
//    compile 'com.squareup.sqlbrite:sqlbrite:0.6.2'

    // Dependencies for local unit tests
    testCompile "junit:junit:$rootProject.ext.junitVersion"
}
